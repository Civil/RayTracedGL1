// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#version 460

#define DESC_SET_FRAMEBUFFERS 0
#define DESC_SET_GLOBAL_UNIFORM 1
#define DESC_SET_TONEMAPPING 2
#include "ShaderCommonGLSLFunc.h"

layout(local_size_x = COMPUTE_COMPOSE_GROUP_SIZE_X, local_size_y = COMPUTE_COMPOSE_GROUP_SIZE_Y, local_size_z = 1) in;

void main()
{
    ivec2 pix = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);

    if (pix.x >= uint(globalUniform.renderWidth) || pix.y >= uint(globalUniform.renderHeight))
    {
        return;
    }

    vec3 color = texelFetch(framebufFinal_Sampler, pix, 0).rgb;

    const float lIn = max(getLuminance(color), exp2(globalUniform.minLogLuminance));
    const float lInAdj = lIn / tonemapping.avgLuminance;
    // Reinhard
    const float lOut = lInAdj * (1.0f + (lInAdj / (globalUniform.luminanceWhitePoint * globalUniform.luminanceWhitePoint))) / (1.0f + lInAdj);
    
    color = color * lOut / lIn;

    if (globalUniform.dbgShowGradients != 0)
    {
        const vec4 gradDS = texelFetch(framebufDiffAndSpecPingGradient_Sampler, pix / COMPUTE_ASVGF_STRATA_SIZE, 0);
        const vec4 gradI = texelFetch(framebufIndirPingGradient_Sampler, pix / COMPUTE_ASVGF_STRATA_SIZE, 0);
        const float lambda = clamp(abs(gradDS.r) / gradDS.g, 0.0, 1.0);
        const float lambdaSpec = clamp(abs(gradDS.b) / gradDS.a, 0.0, 1.0);
        const float lambdaIndir = clamp(abs(gradI.r) / gradI.g, 0.0, 1.0);
        color.rgb = vec3(lambda, lambdaSpec, lambdaIndir);
    }

    if (globalUniform.dbgShowMotionVectors != 0)
    {
        const vec2 m = texelFetch(framebufMotion_Sampler, pix, 0).rg;
        color.rg = m;
        color.b = 0;
    }
        
    imageStore(framebufFinal, pix, vec4(pow(color, vec3(1 / 2.2)), 0));
}