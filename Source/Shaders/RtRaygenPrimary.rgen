// Copyright (c) 2021 Sultim Tsyrendashiev
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#version 460
#extension GL_EXT_ray_tracing : require

#define DESC_SET_FRAMEBUFFERS 1
#define DESC_SET_GLOBAL_UNIFORM 2
#define DESC_SET_VERTEX_DATA 3
#define DESC_SET_TEXTURES 4
#define DESC_SET_RANDOM 5
#include "ShaderCommonGLSLFunc.h"

layout(binding = BINDING_ACCELERATION_STRUCTURE, set = 0) uniform accelerationStructureEXT topLevelAS;

layout(location = PAYLOAD_INDEX_DEFAULT) rayPayloadEXT ShPayload payload;

void processPrimaryHit(vec3 hitPosition, ivec2 pix)
{
    int instanceId, instCustomIndex;
    int geomIndex, primIndex;

    unpackInstanceIdAndCustomIndex(payload.instIdAndIndex, instanceId, instCustomIndex);
    unpackGeometryAndPrimitiveIndex(payload.geomAndPrimIndex, geomIndex, primIndex);

    ShTriangle tr = getTriangle(instanceId, instCustomIndex, geomIndex, primIndex);
    mat4 model = getModelMatrix(instanceId, instCustomIndex, geomIndex);

    vec2 inBaryCoords = payload.baryCoords;
    vec3 baryCoords = vec3(1.0f - inBaryCoords.x - inBaryCoords.y, inBaryCoords.x, inBaryCoords.y);
    
    
    vec2 texCoords[] = 
    {
        tr.layerTexCoord[0] * baryCoords,
        tr.layerTexCoord[1] * baryCoords,
        tr.layerTexCoord[2] * baryCoords
    };

    
    vec3 albedo;
    vec3 normal;
    float metallic;
    vec3 emission;
    float roughness;
    

    albedo = tr.geomColor.rgb;

    if (tr.materials[0][MATERIAL_ALBEDO_ALPHA_INDEX] != MATERIAL_NO_TEXTURE)
    {
        albedo *= getTextureSample(tr.materials[0][MATERIAL_ALBEDO_ALPHA_INDEX], texCoords[0]).rgb;
    }
    if (tr.materials[1][MATERIAL_ALBEDO_ALPHA_INDEX] != MATERIAL_NO_TEXTURE)
    {
        albedo *= getTextureSample(tr.materials[1][MATERIAL_ALBEDO_ALPHA_INDEX], texCoords[1]).rgb;
    }   
    if (tr.materials[2][MATERIAL_ALBEDO_ALPHA_INDEX] != MATERIAL_NO_TEXTURE)
    {
        albedo *= getTextureSample(tr.materials[2][MATERIAL_ALBEDO_ALPHA_INDEX], texCoords[2]).rgb;
    }
    

    // convert normals to world space
    tr.normals[0] = vec3(model * vec4(tr.normals[0], 0.0));
    tr.normals[1] = vec3(model * vec4(tr.normals[1], 0.0));
    tr.normals[2] = vec3(model * vec4(tr.normals[2], 0.0));

    vec3 normalGeom = normalize(tr.normals * baryCoords);


    if (tr.materials[0][MATERIAL_NORMAL_METALLIC_INDEX] != MATERIAL_NO_TEXTURE)
    {
        vec4 nm = getTextureSample(tr.materials[0][MATERIAL_NORMAL_METALLIC_INDEX], texCoords[0]);
        metallic = nm.a;

        // TODO: normal maps, tangents
        normal = normalGeom;
    }
    else
    {
        normal = normalGeom;
        metallic = tr.geomMetallicity;
    }
    

    if (tr.materials[0][MATERIAL_EMISSION_ROUGHNESS_INDEX] != MATERIAL_NO_TEXTURE)
    {
        vec4 er = getTextureSample(tr.materials[0][MATERIAL_EMISSION_ROUGHNESS_INDEX], texCoords[0]);
        emission = er.rgb;
        roughness = er.a;
    }
    else
    {
        emission = tr.geomEmission;
        roughness = tr.geomRoughness;
    }


    imageStore(framebufAlbedo,              pix, vec4(albedo, 0));
    imageStore(framebufNormal,              pix, vec4(normal, 0));
    imageStore(framebufNormalGeometry,      pix, vec4(normalGeom, 0));
    imageStore(framebufMetallicRoughness,   pix, vec4(metallic, max(roughness, 0.001), 0, 0));
    imageStore(framebufDepth,               pix, vec4(payload.clsHitDistance));
    imageStore(framebufSurfacePosition,      pix, vec4(hitPosition, 0.0));
}

void main() 
{
    const ivec2 pix = ivec2(gl_LaunchIDEXT.xy);
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    vec4 origin = globalUniform.invView * vec4(0, 0, 0, 1);
    vec4 target = globalUniform.invProjection * vec4(d.x, d.y, 1, 1);
    vec4 viewDirection = globalUniform.invView * vec4(normalize(target.xyz / target.w), 0);

    imageStore(framebufViewDirection, pix, viewDirection);

    uint randomSeed = getRandomSeed(pix, globalUniform.frameId, globalUniform.renderWidth, globalUniform.renderHeight);
    imageStore(framebufRandomSeed, pix, uvec4(randomSeed));

    // start color is fully transparent black and dist = 0,
    // so the first tranparent-blended (additive or under) surface 
    // could overwrite these values
    payload.color = vec4(0.0);
    payload.baryCoords = vec2(0.0);
    payload.instIdAndIndex = 0;
    payload.geomAndPrimIndex = 0;
    payload.clsHitDistance = -1;
    payload.maxTransparDistance = -1;
    
    traceRayEXT(
        topLevelAS,
        gl_RayFlagsNoneEXT, 
        INSTANCE_MASK_ALL, 
        0, 0,     // sbtRecordOffset, sbtRecordStride
        SBT_INDEX_MISS_DEFAULT, 
        origin.xyz, 0.001, viewDirection.xyz, MAX_RAY_LENGTH, 
        PAYLOAD_INDEX_DEFAULT);    

    vec4 color;

    // was hit
    if (payload.clsHitDistance > 0)
    {
        vec3 clsHitPoint = origin.xyz + viewDirection.xyz * payload.clsHitDistance;

        // if transparency hit distance is further than the closest hit,
        // then need to cast again, but new ray's max distance is clsHitDistance
        if (payload.maxTransparDistance > payload.clsHitDistance)
        {
            payload.color = vec4(0.0);
            payload.maxTransparDistance = -1;
            
            // trace again, without closest hit shader,
            // so any-hit will gather transparency info
            traceRayEXT(
                topLevelAS,
                gl_RayFlagsNoneEXT, 
                INSTANCE_MASK_ALL, 
                0, 0,     // sbtRecordOffset, sbtRecordStride
                SBT_INDEX_MISS_DEFAULT, 
                origin.xyz, 0.001, viewDirection.xyz, payload.clsHitDistance, 
                PAYLOAD_INDEX_DEFAULT);    
        }
        
        processPrimaryHit(origin.xyz + viewDirection.xyz * payload.clsHitDistance, pix);
    }
    else
    {
        // sky
        color = vec4(0.85, 0.95, 1.0, 1.0);

        imageStore(framebufAlbedo,              pix, color);
        imageStore(framebufNormal,              pix, vec4(0.0));
        imageStore(framebufNormalGeometry,      pix, vec4(0.0));
        imageStore(framebufMetallicRoughness,   pix, vec4(0, 0, 0, 0));
        imageStore(framebufDepth,               pix, vec4(MAX_RAY_LENGTH));
        // imageStore(framebufSurfacePosition,      pix, vec4(0.0));
    }
}
